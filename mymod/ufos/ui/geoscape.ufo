// ==================
// MAP MENU
// ==================

component cmpGeoscapeDocklet extends panel {
	{
		pos		"0 0"
		size	"280 60"
		align	LAYOUTALIGN_TOP
	}

	image bg {
		src				"geoscape/docklet_bg"
		pos				"0 1"
		size			"280 60"
		onMouseEnter	{ *node:this@src = "geoscape/docklet_bg_hovered" }
		onMouseLeave	{ *node:this@src = "geoscape/docklet_bg" }
		onClick			{ cmd "map_selectobject <node:parent.type@string> <node:parent.id@string>;" }
	}

	model siteModel {
		pos		"5 2"
		size	"50 56"
		autoscale	true
		ghost	true
		angles	"0 0 160"
	}

	string	message {
		pos		"65 5"
		size	"210 50"
		ghost	true
	}

	/** store the id to find the object */
	data type { }

	/** store the id to find the object */
	data id { }
}

// ================
// GEOSCAPE WINDOW
// ================

window geoscape
{
	{
		fill		true
		starlayout	true
	}

	geoscape themap
	{
		pos				"0 0"
		size			"1024 768"
		onRClick		{ cmd "mn_mapaction_reset;" }
		align			LAYOUTALIGN_FILL
		padding-right	256
	}

	confunc clean_geoscape_object {
		call *node:root.messages.dock@removeAllChild
	}

	/**
	 * @param<1> type [mission|ufo]
	 * @param<2> id (internal, to identify the docklet object)
	 * @param<3> model
	 * @param<4> text
	 */
	confunc add_geoscape_object
	{
		call *node:root.messages.dock@createChild ( "<1><2>" , "cmpGeoscapeDocklet" )
		*node:root.messages.dock.<1><2>.type@string = <1>
		*node:root.messages.dock.<1><2>.id@string = <2>
		*node:root.messages.dock.<1><2>.siteModel@src = <3>
		*node:root.messages.dock.<1><2>.message@string = <4>
	}

	confunc messagesystem_changemode
	{
		/* one line */
		if ( *cvar:mn_messagesystem == 0 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height	= 20
			*node:root.messages.messagesystem.scroll@invis = true
		}
		/* 6 lines */
		if ( *cvar:mn_messagesystem == 1 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height	= 120
			*node:root.messages.messagesystem.scroll@invis = true
		}
		/* 12 lines */
		if ( *cvar:mn_messagesystem == 2 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height	= 240
			*node:root.messages.messagesystem.scroll@invis = true
		}
		/* full screen */
		if ( *cvar:mn_messagesystem == 3 ) {
			*node:root.messages.messagesystem@width = ( *cvar:ui_sys_screenwidth - 256 )
			*cvar:foo = ( *cvar:ui_sys_screenwidth - 256 )
			*node:root.messages.messagesystem@height = 768
			*node:root.messages.messagesystem.scroll@invis = false
			cmd "game_timestop;"
		}

	}

	// ===================
	// RIGHT (MAIN) PANEL
	// ===================

	panel control {
		{
			size		"216 728"
			align		LAYOUTALIGN_RIGHT
			border		"2"
			bordercolor	"0.56 0.81 0.76 0.4"
			bgcolor		"0.05 0.08 0.07 0.6"
		}

/*
		panel border {
			{
				pos			"0 70"
				size		"216 465"
				border		"2"
				bordercolor	"0.56 0.81 0.76 1"
			}
		}
*/

		image logo
		{ 
			src			"phalanx_earth_gray.png"
			pos			"44 26"
		}

		string date
		{
			string		"*cvar:mn_mapdate"
			pos			"10 168"
			size		"195 20"
			contentalign	ALIGN_UC
			color		"0.56 0.81 0.76 1"
			font		"f_normal"
		}

		string datetime
		{
			string		"*cvar:mn_maptime"
			pos			"11 190"
			size		"195 30"
			contentalign	ALIGN_UC
			color		"0.80 0.96 0.94 1"
			font		"f_verybig"
		}

		string timelapse
		{
			string		"*cvar:mn_timelapse"
			pos			"11 218"
			size		"195 20"
			contentalign	ALIGN_UC
			color		"0.52 0.76 0.71 1"
			font		"f_verysmall"
		}

		button bt_messagesystem
		{
			// @todo: better -more verbose- tooltip
			tooltip		"_Open up messages"
			padding		0
			icon		"icons/change_height"
			pos			"5 5"
			size		"23 23"
			onClick	{
				cmd "add mn_messagesystem 1;"
				cmd "mod mn_messagesystem 4;"
				cmd "messagesystem_changemode"
			}
			onRClick	{
				*cvar:mn_messagesystem = 3
				cmd "messagesystem_changemode"
			}
		}

		// ==================
		// credits strings
		// ==================

		string txt_credits_caption
		{
			string		"_CREDITS"
			pos			"11 270"
			size		"97 25"
			font		"f_small"
			contentalign	ALIGN_CC
		}

		string txt_credits
		{
			string		"*cvar:mn_credits"
			tooltip		"_Organisation capital"
			pos			"104 270"
			size		"97 25"
			font		"f_small"
			contentalign	ALIGN_CC
		}
		// ==================
		// timecontrol panel
		// ==================

		panel time_panel {
			{
				pos		"0 284"
				size	"214 55"
			}

			func faster {
				*cvar:mn_fastexit = 0;
				cmd "game_timefast;"
			}
			func slower {
				*cvar:mn_fastexit = 0;
				cmd "game_timeslow;"
			}

			radiobutton bt_timestop0
			{
				tooltip		"_Pause"
				cvar		*cvar:mn_timelapse_id	value "0"
				icon		"icons/speed_stop"
				pos			"28 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ *cvar:mn_fastexit = 0; cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay1
			{
				tooltip		"_Time lapse: 5 seconds"
				cvar		*cvar:mn_timelapse_id	value "1"
				icon		"icons/speed_dot"
				pos			"48 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay2
			{
				tooltip		"_Time lapse: 5 minutes"
				cvar		*cvar:mn_timelapse_id	value "2"
				icon		"icons/speed_play1"
				pos			"68 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay3
			{
				tooltip		"_Time lapse: 20 minutes"
				cvar		*cvar:mn_timelapse_id	value "3"
				icon		"icons/speed_dot"
				pos			"88 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay4
			{
				tooltip		"_Time lapse: 1 hour"
				cvar		*cvar:mn_timelapse_id	value "4"
				icon		"icons/speed_play2"
				pos			"108 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay5
			{
				tooltip		"_Time lapse: 12 hours"
				cvar		*cvar:mn_timelapse_id	value "5"
				icon		"icons/speed_dot"
				pos			"128 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay6
			{
				tooltip		"_Time lapse: 1 day"
				cvar		*cvar:mn_timelapse_id	value "6"
				icon		"icons/speed_dot"
				pos			"148 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

			radiobutton bt_timeplay7
			{
				tooltip		"_Time lapse: 5 days"
				cvar		*cvar:mn_timelapse_id	value "7"
				icon		"icons/speed_play3"
				pos			"168 10"
				size		"20 20"
				onChange	{ *cvar:mn_fastexit = 0; cmd "game_settimeid <value>;" }
//				onWheelUp	{ cmd "game_timefast;" }
//				onWheelDown	{ cmd "game_timeslow;" }
			}

		}

		// ====================
		// mission description
		// ====================

		text description
		{
			dataid		TEXT_STANDARD
			pos			"22 365"
			size		"172 144"
			lineheight	18
			tabwidth	70
			rows		8
			onViewChange {
				*node:parent.description_scroll@fullsize = <fullsize>
				*node:parent.description_scroll@current = <viewpos>
				*node:parent.description_scroll@viewsize = <viewsize>
			}
		}

		vscrollbar description_scroll {
			image		"ui/scrollbar_v"
			pos			"185 362"
			height		"144"
			current		0
			viewsize	8
			fullsize	8
			hidewhenunused	true
			onChange {
				*node:parent.description@viewpos	 = <current>
			}
		}

		// =====================
		// day/nighttime string
		// =====================

		string daytime
		{
			string		"*cvar:mn_mapdaytime"
			pos			"11 343"
			size		"195 20"
			contentalign	ALIGN_UC
		}


		// ==================
		// buttons
		// ==================


		button bt_mailclient
		{
			visiblewhen "*cvar:mn_upunreadmail != 0"

			string		"_Mailclient"
			tooltip		"_INBOX (with unread mails)"
			color		"0.56 0.81 0.76 1"
			font		"f_small"
			pos			"21 478"
			flash		true
			size		"175 21"
			selectcolor	"1 1 1 1"
			background		"ui/button_geoscape"

			onClick		{ cmd "ui_push mailclient;" }
		}

		button bt_mailclient_nomail
		{
			visiblewhen "*cvar:mn_upunreadmail == 0"

			string		"_Mailclient"
			tooltip		"_INBOX"
			color		"0.56 0.81 0.76 1"
			font		"f_small"
			pos			"21 478"
			size		"175 21"
			selectcolor	"1 1 1 1"
			background	"ui/button_geoscape"

			onClick		{ cmd "ui_push mailclient;" }
		}

		button bt_stats
		{
			string		"_Statistics"
			tooltip		"_Organisation statistics"
			pos			"21 500"
			size		"175 21"
			color		"0.56 0.81 0.76 1"
			font		"f_small"
			selectcolor	"1 1 1 1"
			background	"ui/button_geoscape"

			onClick		{ cmd "ui_push stats;" }
		}

		button bt_nations
		{
			string		"_Nations"
			tooltip		"_Nation information"
			pos			"21 522"
			size		"175 21"
			color		"0.56 0.81 0.76 1"
			font		"f_small"
			selectcolor	"1 1 1 1"
			background	"ui/button_geoscape"

			onClick		{ cmd "ui_push nations;" }
		}

		button bt_ufopedia
		{
			string		"_UFOpaedia"
			pos			"21 544"
			size		"175 21"
			color		"0.56 0.81 0.76 1"
			font		"f_small"
			selectcolor	"1 1 1 1"
			background	"ui/button_geoscape"

			onClick		{ cmd "ui_push ufopedia;" }
		}

		button bt_installation
		{
			string		"_Create installation"
			color		"0.56 0.81 0.76 1"
			pos			"21 566"
			size		"175 21"
			font		"f_small"
			selectcolor	"1 1 1 1"
			background	"ui/button_geoscape"

			onClick		{ cmd "ui_push popup_newinstallation_type;" }
		}

		// ==================
		// small buttons
		// ==================

		button bt_show_overlay {
			tooltip		"_Highlight nation borders"
			icon		"icons/geo_nations"
			pos		"20 659"
			size		"32 32"
			background	"icons/geo_button"
			onMouseEnter { *node:this@background = "icons/geo_button_hvrd" }
			onMouseLeave { *node:this@background = "icons/geo_button" }
			onClick {
				if ( *cvar:geo_overlay_nation > 0 ) {
					cmd "geo_setoverlay nation 0;"
				} else {
					cmd "geo_setoverlay nation 1;"
				}
			}
		}

		button bt_show_radar
		{
			tooltip		"_Show radar range"
			icon		"icons/geo_radar"
			pos			"50 659"
			size		"32 32"
			background	"icons/geo_button"
			onMouseEnter { *node:this@background = "icons/geo_button_hvrd" }
			onMouseLeave { *node:this@background = "icons/geo_button" }
			onClick	{
				if ( *cvar:geo_overlay_radar > 0 ) {
					cmd "geo_setoverlay radar 0;"
				} else {
					cmd "geo_setoverlay radar 1;"
				}
			}
		}

		button bt_show_xvi
		{
			visiblewhen	"*cvar:mn_xvimap == 1"
			tooltip		"_Show XVI map"
			icon		"icons/geo_xvi"
			pos			"80 659"
			size		"32 32"
			background	"icons/geo_button"
			onMouseEnter { *node:this@background = "icons/geo_button_hvrd" }
			onMouseLeave { *node:this@background = "icons/geo_button" }
			onClick		{
				if ( *cvar:geo_overlay_xvi > 0 ) {
					cmd "geo_setoverlay xvi 0;"
				} else {
					cmd "geo_setoverlay xvi 1;"
				}
			}
		}

		confunc ui_update_overlay_buttons {
			if ( *cvar:geo_overlay_radar > 0 ) {
				*node:parent.bt_show_radar@icon = "icons/geo_radar_on"
			} else {
				*node:parent.bt_show_radar@icon = "icons/geo_radar"
			}
			if ( *cvar:geo_overlay_nation > 0 ) {
				*node:parent.bt_show_overlay@icon = "icons/geo_nations_on"
			} else {
				*node:parent.bt_show_overlay@icon = "icons/geo_nations"
			}
			if ( *cvar:geo_overlay_xvi > 0 ) {
				*node:parent.bt_show_xvi@icon = "icons/geo_xvi_on"
			} else {
				*node:parent.bt_show_xvi@icon = "icons/geo_xvi"
			}
		}

		button bt_mapcenter
		{
			tooltip		"_Center objects on the map"
			icon		"icons/geo_center"
			pos			"140 659"
			size		"32 32"
			background	"icons/geo_button"
			onMouseEnter { *node:this@background = "icons/geo_button_hvrd" }
			onMouseLeave { *node:this@background = "icons/geo_button" }
			onClick		{ cmd "map_center;" }
		}

		button bt_menu
		{
			tooltip		"_Go to Options"
			icon		"icons/geo_options"
			pos			"170 659"
			size		"32 32"
			background	"icons/geo_button"
			onMouseEnter { *node:this@background = "icons/geo_button_hvrd" }
			onMouseLeave { *node:this@background = "icons/geo_button" }
			onClick		{ cmd "ui_pop;" }
		}

		button bt_messagesettings
		{
			string		"_Autopause settings"
			tooltip		"_Change message and pause settings"
			color		"0.56 0.64 0.58 1"
			font	 	"f_tiny"
			pos			"13 692"
			size		"112 21"
			selectcolor	"0.9 0.9 1 1"
			background	"ui/button_geoscape"
			onClick		{ cmd "ui_push messageoptions;" }
		}

	}

	// ==================
	// Messages
	// ==================

	panel messages {
		{
			align	LAYOUTALIGN_FILL
			layout	LAYOUT_PACK
			ghost	true
			size	"1024 768"
		}

		panel rightMargin {
			align	LAYOUTALIGN_RIGHT
			size	"256 30"
			ghost	true
		}

		panel messagesystem {
			{
				pos		"0 0"
				size	"768 20"
				layout	LAYOUT_BORDER
				align	LAYOUTALIGN_TOP
			}

			vscrollbar scroll {
				align		LAYOUTALIGN_RIGHT
				invis		true
				image		"ui/scrollbar_v"
				bgcolor		"0 0 0 .5"
				pos			"749 0"
				height		"768"
				current		0
				viewsize	38
				fullsize	50
				hidewhenunused true
				onChange	{
					*node:parent.message@viewpos	 = <current>
				}
			}

			messagelist message
			{
				pos			"0 0"
				size		"768 20"
				bgcolor		"0 0 0 .5"
				align		LAYOUTALIGN_MIDDLE
				onMouseEnter {
					if ( *cvar:mn_messagesystem < 2 ) {
						*node:parent@height = 240
					}
				}
				onMouseLeave {
					if ( *cvar:mn_messagesystem < 2 ) {
						cmd "messagesystem_changemode"
				// Temporary fix
						if ( *cvar:mn_messagesystem == 0 ) { *node:parent@height = 20 }
						if ( *cvar:mn_messagesystem == 1 ) { *node:parent@height = 120 }
						if ( *cvar:mn_messagesystem == 2 ) { *node:parent@height = 256 }
						if ( *cvar:mn_messagesystem == 3 ) { *node:parent@height = 768 }
				// end
					}

				}
				onViewChange {
					*node:parent.scroll@fullsize = <fullsize>
					*node:parent.scroll@current = <viewpos>
					*node:parent.scroll@viewsize = <viewsize>
				}
			}
		}

		panel dock {
			{
				pos				"0 0"
				size			"301 768"
				align			LAYOUTALIGN_LEFT
				layout			LAYOUT_PACK
				ghost			true
				padding			10
				layoutmargin	10
			}
		}
	}

	func onWindowOpened {
		cmd "music_change geoscape;"

		if ( not exists *cvar:mn_messagesystem ) {
			*cvar:mn_messagesystem = 0
			cmd "messagesystem_changemode;"
		}

		cmd "ui_update_overlay_buttons;"
		cmd "ui_addcvarlistener geo_overlay_radar ui_update_overlay_buttons;"
		cmd "ui_addcvarlistener geo_overlay_nation ui_update_overlay_buttons;"
		cmd "ui_addcvarlistener geo_overlay_xvi ui_update_overlay_buttons;"
	}

	func onWindowClosed {
		cmd "ui_removecvarlistener geo_overlay_radar ui_update_overlay_buttons;"
		cmd "ui_removecvarlistener geo_overlay_nation ui_update_overlay_buttons;"
		cmd "ui_removecvarlistener geo_overlay_xvi ui_update_overlay_buttons;"
	}
}
